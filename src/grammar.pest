groups = _{ selection ~ ("," ~ selection)* }
    selection = { (spread? ~ root? ~ (selector ~ ("."? ~ selector)*)?)? ~ filters* }
        root = { "." }

        spread = { ".." }

        selector = _{ array | object | default }
            array = _{ "[" ~ (range | index) ~ "]" }
                range = { ASCII_DIGIT+ ~ ":" ~ ASCII_DIGIT+ }
                index = { (ASCII_DIGIT+)? ~ ("," ~ ASCII_DIGIT+)* }
            object = _{ "{" ~ property ~ "}" }
                property = { default ~ ("," ~ default)* }
            default = { quote ~ char* ~ quote }
                char = {
                    !("\"" | "\\") ~ ANY
                    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
                    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
                }
                quote = { "\"" }

        filters = _{ pipe ~ filter ~ ("."? ~ filter)* }
            pipe = _{ "|" }
            filter = _{ filter_array | filter_default }
                filter_array = _{ "[" ~ (filter_range | filter_index) ~ "]" }
                    filter_range = { range }
                    filter_index = { index }
                filter_default = { default }
